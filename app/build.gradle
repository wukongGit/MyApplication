apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMddHHmmss", TimeZone.getTimeZone("PRC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "com.suncheng.myapplication"
        minSdkVersion 15
        targetSdkVersion 23
        versionName "1.1"
    }

    signingConfigs {
        release {
            def propsFile = rootProject.file('pro.properties')
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            storeFile = file(props['key.store'])
            storePassword = props['key.store.password']
            keyAlias = props['key.alias']
            keyPassword = props['key.alias.password']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }

        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk') && !outputFile.name.contains('debug')) {
                        // 输出apk名称为xxx_v1.0_2015-01-15_wandoujia.apk  xxx可以进行自定义
                        def fileName = "pro${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors {
        GooglePlay{}
        androidmarket{}
    }
    //批量配置
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    //compile project(':nohttp')
    compile project(':imageLoader')
    compile project(':crashcatch')
    compile files('libs/jsoup-1.7.2.jar')
    compile project(':pullToRefresh')
    // https://github.com/anthonycr/Grant
    compile 'com.anthonycr.grant:permissions:1.0'

//    compile 'org.greenrobot:greendao:3.0.1'
//    compile 'org.greenrobot:greendao-generator:3.0.0'
    compile 'com.tencent.bugly:crashreport:latest.release' //其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.1.5
    compile 'com.umeng.analytics:analytics:latest.integration'
}
